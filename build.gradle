plugins {
    id "java"
    id "com.github.spotbugs" version "4.7.3" apply false
    id "org.owasp.dependencycheck" version "8.1.2" apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

allprojects {
    group = 'uk.co.autotrader'
    version = "1.0.0"
}


project(':traverson4j-core').description("The kernel of traverson4j. This provides the main API for a client to traverse a Hypermedia REST service")
project(':traverson4j-hc5').description("An Apache HttpComponents 5 client to power traverson4j-core")
project(':traverson4j-jackson2').description("A ResourceConverter backed by Jackson 2 binding")

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2',
                'org.junit.jupiter:junit-jupiter-params:5.9.2',
                'org.junit.jupiter:junit-jupiter-engine:5.9.2',
                'com.google.guava:guava:31.1-jre',
                'org.mockito:mockito-core:5.1.1',
                'org.mockito:mockito-junit-jupiter:5.1.1',
                'org.assertj:assertj-core:3.24.2',
                'org.apache.commons:commons-lang3:3.12.0'
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }
            }
        }
    }

    spotbugs {
        toolVersion = '4.2.3'
    }

    build {
        dependsOn('jacocoTestCoverageVerification')
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = "8.41.1"
    }

    jar {
        from('../LICENSE') {
            into('META-INF/')
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    signing {
        def signingKeyId = findProperty("signingKeyId")
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications
    }

    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "${project.name}"
                    description = project.description
                    url = 'https://github.com/autotraderuk/traverson4j'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'MikeRocke'
                            name = 'Michael Rocke'
                            email = 'Michael.Rocke@autotrader.co.uk'
                            organization = "Auto Trader"
                            organizationUrl = "https://www.autotrader.co.uk/"
                        }
                        developer {
                            id = 'shaunstorey'
                            name = 'Shaun Storey'
                            email = 'Shaun.Storey@autotrader.co.uk'
                            organization = "Auto Trader"
                            organizationUrl = "https://www.autotrader.co.uk/"
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/autotraderuk/traverson4j.git'
                        developerConnection = 'scm:git:ssh://github.com/autotraderuk/traverson4j.git'
                        url = 'https://github.com/autotraderuk/traverson4j'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

project(':traverson4j-hc5') {
    dependencies {
        api project(':traverson4j-core')
    }
}

project(':traverson4j-jackson2') {
    dependencies {
        api project(':traverson4j-core')
    }
}
