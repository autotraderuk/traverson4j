plugins {
    id "java"
    id "com.github.spotbugs" version "6.4.4" apply false
    id "org.owasp.dependencycheck" version "12.1.3" apply false
    id "maven-publish"
    id 'org.jreleaser' version '1.20.0'
}

allprojects {
    group = 'uk.co.autotrader'
    version = "4.3.5"
}


project(':traverson4j-core').description("The kernel of traverson4j. This provides the main API for a client to traverse a Hypermedia REST service")
project(':traverson4j-hc5').description("An Apache HttpComponents 5 client to power traverson4j-core")
project(':traverson4j-jackson2').description("A ResourceConverter backed by Jackson 2 binding")

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'org.owasp.dependencycheck'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4',
                'org.junit.jupiter:junit-jupiter-params:5.13.4',
                'org.junit.jupiter:junit-jupiter-engine:5.13.4',
                'com.google.guava:guava:33.4.8-jre',
                'org.mockito:mockito-core:5.17.0',
                'org.mockito:mockito-junit-jupiter:5.19.0',
                'org.assertj:assertj-core:3.27.6',
                'org.apache.commons:commons-lang3:3.18.0'

        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }
            }
        }
    }

    spotbugs {
        toolVersion = '4.8.6'
        excludeFilter = file("${rootDir}/exclude.xml")
        reportsDir = file("${rootDir}/spotbugs")
    }

    build {
        dependsOn('jacocoTestCoverageVerification')
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = "10.20.2"
    }

    jar {
        from('../LICENSE') {
            into('META-INF/')
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
            repositories {
                maven {
                    url = rootProject.layout.buildDirectory.dir('staging-deploy')
                }
            }

        publications {
            MavenPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "${project.name}"
                    description = project.description
                    url = 'https://github.com/autotraderuk/traverson4j'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'MikeRocke'
                            name = 'Michael Rocke'
                            email = 'Michael.Rocke@autotrader.co.uk'
                            organization = "Auto Trader"
                            organizationUrl = "https://www.autotrader.co.uk/"
                        }
                        developer {
                            id = 'shaunstorey'
                            name = 'Shaun Storey'
                            email = 'Shaun.Storey@autotrader.co.uk'
                            organization = "Auto Trader"
                            organizationUrl = "https://www.autotrader.co.uk/"
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/autotraderuk/traverson4j.git'
                        developerConnection = 'scm:git:ssh://github.com/autotraderuk/traverson4j.git'
                        url = 'https://github.com/autotraderuk/traverson4j'
                    }
                }
            }
        }
    }
}

jreleaser {
  release{
    github {
        skipRelease=true
    }
  }
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}


project(':traverson4j-hc5') {
    dependencies {
        api project(':traverson4j-core')
    }
}

project(':traverson4j-jackson2') {
    dependencies {
        api project(':traverson4j-core')
    }
}
